plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.6' apply false
	id 'io.spring.dependency-management' version '1.1.7' apply false
}

group = 'com.suhoi'
version = '0.0.1-SNAPSHOT'
description = 'CryptoPlatform (multi-module)'

ext {
	versions = [
			junit: '5.10.3'
	]
	springDotenv = 'me.paulschwarz:spring-dotenv:4.0.0'
}

allprojects {
	repositories { mavenCentral() }
}

subprojects {
	apply plugin: 'java'
	java { toolchain { languageVersion = JavaLanguageVersion.of(21) } }
	tasks.withType(Test).configureEach { useJUnitPlatform() }
}

/**
 * Библиотечные модули (core + adapters + persistence) — без Spring Boot
 */
configure([
		project(':modules:core'),
		project(':modules:persistence'),
		project(':modules:adapters:binance-adapter'),
		project(':modules:adapters:bybit-adapter'),
		project(':modules:adapters:bitget-adapter'),
		project(':modules:adapters:gate-adapter'),
		project(':modules:adapters:mexc-adapter'),
		project(':modules:adapters:dexscreener-adapter')
]) {
	apply plugin: 'java-library'

	dependencies {
		testImplementation "org.junit.jupiter:junit-jupiter-api:${versions.junit}"
		testRuntimeOnly  "org.junit.jupiter:junit-jupiter-engine:${versions.junit}"
	}
}

def appModulesWithDb = [
		project(':modules:db-migration-service'),
		project(':modules:discovery-service'),

]

def appModulesNoDb = [
		project(':modules:api-service'),
		project(':modules:stream-router'),
		project(':modules:fairprice-service'),
		project(':modules:bias-service'),
		project(':modules:detector-service'),
		project(':modules:notifier-service')
]

configure(appModulesWithDb) {
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	dependencies {
		implementation project(':modules:core')
		implementation project(':modules:persistence')
		implementation 'org.springframework.boot:spring-boot-starter-web'
		implementation 'org.springframework.boot:spring-boot-starter'
		runtimeOnly  'org.postgresql:postgresql'

		compileOnly 'org.projectlombok:lombok:1.18.34'
		annotationProcessor 'org.projectlombok:lombok:1.18.34'
		testCompileOnly 'org.projectlombok:lombok:1.18.34'
		testAnnotationProcessor 'org.projectlombok:lombok:1.18.34'

		testImplementation 'org.springframework.boot:spring-boot-starter-test'
	}
}

configure(appModulesNoDb) {
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	dependencies {
		implementation project(':modules:core')
		implementation 'org.springframework.boot:spring-boot-starter-web'
		implementation 'org.springframework.boot:spring-boot-starter-actuator'
		implementation 'org.springframework.boot:spring-boot-starter-validation'
		// api-service использует Redis → добавим реальный стартер Redis
		implementation 'org.springframework.boot:spring-boot-starter-data-redis'

		// НИКАКОГО persistence / JPA тут быть не должно

		compileOnly 'org.projectlombok:lombok:1.18.34'
		annotationProcessor 'org.projectlombok:lombok:1.18.34'
		testCompileOnly 'org.projectlombok:lombok:1.18.34'
		testAnnotationProcessor 'org.projectlombok:lombok:1.18.34'

		testImplementation 'org.springframework.boot:spring-boot-starter-test'
	}
}


/** Подключаем dotenv ТОЛЬКО к подпроектам с Spring Boot */
subprojects { subproj ->
	plugins.withId('org.springframework.boot') {
		subproj.dependencies {
			implementation springDotenv
		}
	}
}
